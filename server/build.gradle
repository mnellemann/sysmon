import org.redline_rpm.header.Os

plugins {
    id 'application'
    id "com.palantir.git-version" version "${versioningVersion}"
    id "com.github.johnrengelman.shadow" version "${shadowVersion}"
    id "com.netflix.nebula.ospackage" version "${osPackageVersion}"
}

dependencies {
    testImplementation project(':shared')
    implementation project(':shared')

    annotationProcessor "info.picocli:picocli-codegen:${picocliVersion}"
    implementation "info.picocli:picocli:${picocliVersion}"

    implementation group: 'org.apache.camel', name: 'camel-core', version: camelVersion
    implementation group: 'org.apache.camel', name: 'camel-main', version: camelVersion
    implementation group: 'org.apache.camel', name: 'camel-rest', version: camelVersion
    implementation group: 'org.apache.camel', name: 'camel-jackson', version: camelVersion
    implementation group: 'org.apache.camel', name: 'camel-influxdb', version: camelVersion
    implementation group: 'org.apache.camel', name: 'camel-netty-http', version: camelVersion
}

def projectName = "sysmon-server"

application {
    // Define the main class for the application.
    mainClass.set('sysmon.server.Application')
    applicationDefaultJvmArgs = [ "-Xms64m", "-Xmx64m", "-XX:+ExitOnOutOfMemoryError", "-XX:+AlwaysPreTouch" ]
}

run {
    systemProperty 'sysmon.cfgFile', 'doc/sysmon-server.toml'
    systemProperty 'sysmon.debug', '1'
}

tasks.named('test') {
    // Use junit platform for unit tests.
    useJUnitPlatform()
}

ospackage {
    packageName = projectName
    release = '1'
    user = 'root'
    packager = "Mark Nellemann <mark.nellemann@gmail.com>"

    into '/opt/sysmon/server'

    from(shadowJar.outputs.files) {
        into 'lib'
    }

    from('build/scriptsShadow') {
        into 'bin'
    }

    from('doc/') {
        into 'doc'
    }

    from(['README.md', 'LICENSE']) {
        into 'doc'
    }

}

buildDeb {
    dependsOn startShadowScripts
    installUtils file('../shared/scripts/utils.sh') as File
    installUtils file('scripts/config.sh') as File
    preUninstall file('scripts/deb-pre-rm.sh') as File
    postInstall file('scripts/deb-post-inst.sh') as File
}

buildRpm {
    dependsOn startShadowScripts
    installUtils file('../shared/scripts/utils.sh') as File
    installUtils file('scripts/config.sh') as File
    preUninstall file('scripts/rpm-pre-rm.sh') as File
    postTrans file('scripts/rpm-post-inst.sh') as File
    os Os.LINUX
}



jar {
    manifest {
        attributes(
                'Created-By'     : "Gradle ${gradle.gradleVersion}",
                'Build-OS'       : "${System.properties['os.name']} ${System.properties['os.arch']} ${System.properties['os.version']}",
                'Build-Jdk'      : "${System.properties['java.version']} (${System.properties['java.vendor']} ${System.properties['java.vm.version']})",
                'Build-User'     : System.properties['user.name'],
                'Build-Version'  : gitVersion(),
                'Build-Timestamp': new Date().format("yyyy-MM-dd'T'HH:mm:ss.SSSZ").toString(),
                'Add-Opens'      : 'java.base/java.lang.invoke' // To ignore "Illegal reflective access by retrofit2.Platform" warnings
        )
    }
}

shadowJar {
    archiveBaseName.set(projectName)
    archiveClassifier.set('')
    archiveVersion.set('')
    mergeServiceFiles() // Tell plugin to merge duplicate service files
}
